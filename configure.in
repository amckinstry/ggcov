dnl Process this file with autoconf to produce a configure script.
dnl You may need the --localdir option to get the included .m4's
dnl
dnl ggcov - A GTK frontend for exploring gcov coverage data
dnl Copyright (c) 2001-2003 Greg Banks
dnl 
dnl This program is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation; either version 2 of the License, or
dnl (at your option) any later version.
dnl 
dnl This program is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl 
dnl You should have received a copy of the GNU General Public License
dnl along with this program; if not, write to the Free Software
dnl Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
dnl 
dnl $Id: configure.in,v 1.19 2003-05-29 14:56:06 gnb Exp $
dnl

AC_INIT(src/ggcov.c)
AM_INIT_AUTOMAKE(ggcov, 0.1.2)
AM_CONFIG_HEADER(config.h)

AM_MAINTAINER_MODE

dnl Enabling debugging code
dnl This is mostly straight out of the automake manual.
AC_MSG_CHECKING(whether to enable debugging code)
AC_ARG_ENABLE(debug,
[  --enable-debug          Turn on debugging code],[
case "${enableval}" in
yes|no)
    debug=${enableval}
    ;;
*)
    AC_MSG_ERROR(bad value ${enableval} for --enable-debug)
    ;;
esac
],[debug=no])
AC_MSG_RESULT($debug)
AM_CONDITIONAL(DEBUG, test x$debug = xyes)

dnl Checks for programs.
AC_ARG_PROGRAM
AC_PROG_INSTALL
AC_PROG_AWK
AC_PROG_CC
AC_PROG_CXX

if test x$debug = xyes ; then
##   CFLAGS="$CFLAGS -DGTK_DISABLE_DEPRECATED=1 -DGLIB_DISABLE_DEPRECATED=1"
    echo "debug mode on"
fi

dnl Stuff for gettext
dnl ALL_LINGUAS="la"
dnl AM_GNU_GETTEXT

dnl Checks for libraries.
AC_CHECK_LIB(bfd, bfd_init)
AC_CHECK_LIB(iberty, obstack_free)

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(malloc.h sys/ioctl.h sys/time.h unistd.h memory.h)
AC_CHECK_HEADERS(signal.h sys/filio.h stdint.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_PID_T

dnl Check for the libglade, libxml, and GTK libraries


dnl A bunch of warning options for gcc
dnl -Wshadow
AC_GCC_ADD_CFLAGS([-Wall -Wchar-subscripts -Wmissing-declarations
    -Wmissing-prototypes -Wnested-externs -Wpointer-arithmetic
    -Wcast-align -Wsign-compare -pedantic -Wno-long-long])

dnl Compile C and C++ with the same (C++) compiler.
CXXFLAGS="$CFLAGS"
CC="$CXX"



AC_MSG_CHECKING(whether to enable GTK2)
AC_ARG_ENABLE(gtk2,
[  --enable-gtk2           Turn on support for GTK2],[
case "${enableval}" in
yes|no)
    gtk2=${enableval}
    ;;
*)
    AC_MSG_ERROR(bad value ${enableval} for --enable-gtk)
    ;;
esac
],[gtk2=no])
AC_MSG_RESULT($gtk2)

if test $gtk2 = yes ; then
    # v.1.99.9 shipped in RH 7.3
    PKG_CHECK_MODULES(GGCOV, libglade-2.0 >= 1.99.9)
else
    AM_PATH_LIBGLADE(,AC_MSG_ERROR(ggcov requires the libglade library),gnome)
    AM_PATH_IMLIB(,,AC_MSG_ERROR(ggcov requires the libgdk_imlib library))
    GGCOV_CFLAGS="$LIBGLADE_CFLAGS $IMLIB_CFLAGS"
    GGCOV_LIBS="$LIBGLADE_LIBS $IMLIB_LIBS"
fi
AC_SUBST(GGCOV_CFLAGS)
AC_SUBST(GGCOV_LIBS)


dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_TYPE_SIGNAL
AC_FUNC_WAIT3
AC_CHECK_FUNCS(putenv regcomp strchr)

AC_DEFINE_UNQUOTED(PACKAGE, "$PACKAGE")
AC_DEFINE_UNQUOTED(VERSION, "$VERSION")
AC_DEFINE(HAVE_WAIT3)
dnl AC_SUBST(ALL_LINGUAS)
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)
AC_OUTPUT([
    Makefile
    src/Makefile
    ui/Makefile
    doc/Makefile
    ggcov.spec
    version.m4
])
dnl intl/Makefile po/Makefile.in
